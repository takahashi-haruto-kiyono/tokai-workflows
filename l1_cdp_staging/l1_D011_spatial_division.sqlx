config {
    type: "incremental",
    schema: "l1_cdp_staging",
    description: "ガス供給空調区分M",
    columns: {
        code: "ガス供給空調区分コード",
        name: "ガス供給空調区分名"
    },
    tags: ["l1", "D011", "l1_D011"],
    uniqueKey: ["code"],
    assertions: {
        nonNull: ['code'],
        uniqueKey: ['code'],
    },
}
SELECT DISTINCT
    RTRIM(code) AS code,
    RTRIM(name) AS name
FROM ${ref('l0_D011_spatial_division')}
WHERE 0=0
    ${when(incremental(),
    `AND _PARTITIONTIME = (SELECT MAX(_PARTITIONTIME) FROM ${ref('l0_D011_spatial_division')})`
    )}
QUALIFY ROW_NUMBER()OVER(PARTITION BY code ORDER BY _PARTITIONDATE DESC)=1